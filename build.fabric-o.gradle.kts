plugins {
	id("mod-platform")
	id("net.fabricmc.fabric-loom-remap")
}

stonecutter {
	val (version, loader) = current.project.split('-', limit = 2)
	properties.tags(version, loader)

	replacements.string(current.parsed >= "1.21.11") {
		replace("ResourceLocation", "Identifier")
		replace("location()", "identifier()")
	}
	replacements.string(current.parsed >= "26.1.2") {
		replace("FabricDataOutput", "FabricPackOutput")
	}
}

platform {
	loader = "fabric-o"
	dependencies {
		required("minecraft") {
			fabricLikeVersionRange = if (project.hasProperty("deps.minecraft-range")) prop("deps.minecraft-range") else prop("deps.minecraft")
		}
		required("fabric-api") {
			if (project.hasProperty("deps.fabric-api-id")) modid.set(prop("deps.fabric-api-id"))
			slug("fabric-api")
			fabricLikeVersionRange = ">=${prop("deps.fabric-api")}"
		}
		required("fabricloader") {
			fabricLikeVersionRange = ">=${prop("deps.fabric-loader")}"
		}
		optional("modmenu") {}
	}
}

loom {
	accessWidenerPath.set(
		rootProject.file("src/main/resources/aw/${sc.current.version}.accesswidener").takeIf { it.exists() }
	)
	runs.named("client") {
		client()
		ideConfigGenerated(true)
		runDir = "run/"
		environment = "client"
		programArgs("--username=Dev")
		configName = "Fabric Client"
	}
	runs.named("server") {
		server()
		ideConfigGenerated(true)
		runDir = "run/"
		environment = "server"
		configName = "Fabric Server"
	}
}

fabricApi {
	configureDataGeneration {
		outputDirectory = file("${rootDir}/versions/datagen/${sc.current.version.split("-")[0]}/src/main/generated")
		client = true
	}
}

repositories {
	mavenCentral()
	strictMaven("https://maven.terraformersmc.com/", "com.terraformersmc") { name = "TerraformersMC" }
	strictMaven("https://api.modrinth.com/maven", "maven.modrinth") { name = "Modrinth" }
	maven {
		url = uri("https://maven2.bai.lol")
		content {
			includeGroup("lol.bai")
			includeGroup("mcp.mobius.waila")
		}
	}
}

configurations.all {
	resolutionStrategy {
		force("net.fabricmc:fabric-loader:${prop("deps.fabric-loader")}")
	}
}

dependencies {
	minecraft("com.mojang:minecraft:${prop("deps.minecraft")}")
	mappings(
		loom.layered {
			officialMojangMappings()
			if (hasProperty("deps.parchment")) parchment("org.parchmentmc.data:parchment-${prop("deps.parchment")}@zip")
		})
	modImplementation("net.fabricmc:fabric-loader:${prop("deps.fabric-loader")}")
	// implementation(libs.moulberry.mixinconstraints)
	// include(libs.moulberry.mixinconstraints)
	modImplementation("net.fabricmc.fabric-api:fabric-api:${prop("deps.fabric-api")}")
	modLocalRuntime("com.terraformersmc:modmenu:${prop("deps.modmenu")}")
	if (project.hasProperty("deps.wthit")) {
		val wthitApiVersion = if (project.hasProperty("deps.wthit-api")) prop("deps.wthit-api") else prop("deps.wthit")
		modCompileOnly("mcp.mobius.waila:wthit-api:fabric-$wthitApiVersion")
		modRuntimeOnly("mcp.mobius.waila:wthit:fabric-${prop("deps.wthit")}")
		// BadPackets didn't exist yet for WTHIT releases old enough to still
		// target 1.18/1.18.1 (its own first release postdates WTHIT's last
		// 1.18.1 build by a month, confirmed via Modrinth) - optional here.
		if (project.hasProperty("deps.badpackets")) {
			modRuntimeOnly("lol.bai:badpackets:fabric-${prop("deps.badpackets")}")
		}
	}
}

// waila_plugins.json's schema depends on which WTHIT generation this version
// targets: the legacy PluginLoader only understands "initializer", modern
// ones prefer it over "entrypoints" and take a deprecated registration path
// if it's present at all (see MarkedGoatsWailaPlugin.java) - so a single
// static file can't serve both. Generated per-version instead of static,
// same manifestOutputDir already wired as a resources srcDir by mod-platform.
val wailaPluginsJsonContent = if (project.hasProperty("deps.wthit-legacy-api")) {
	"""{"markedgoats:plugin":{"initializer":"kiwi.allantaylor.markedgoats.MarkedGoatsWailaPlugin","side":"client"}}"""
} else {
	"""{"markedgoats:plugin":{"entrypoints":{"common":"kiwi.allantaylor.markedgoats.MarkedGoatsWailaPlugin","client":"kiwi.allantaylor.markedgoats.MarkedGoatsWailaPlugin"},"side":"client"}}"""
}
val generateWailaPluginsJson = tasks.register("generateWailaPluginsJson") {
	val outputFile = layout.buildDirectory.file("generated/modManifest/waila_plugins.json")
	outputs.file(outputFile)
	doLast {
		val file = outputFile.get().asFile
		file.parentFile.mkdirs()
		file.writeText(wailaPluginsJsonContent)
	}
}
tasks.named("processResources") { dependsOn(generateWailaPluginsJson) }
